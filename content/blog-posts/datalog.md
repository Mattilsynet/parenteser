:page/title En liten titt p친 Datalog
:blog-post/author {:person/id :person/magnar}
:blog-post/published #time/ldt "2024-05-07T09:00:00"
:blog-post/tags [:datomic :clojure]
:blog-post/series {:series/id :datomic}
:blog-post/description

Datalog er et logisk sp칮rrespr친k som ligger et sted mellom Prolog og SQL, noe
som unektelig h칮res ganske esoterisk ut, men som viser seg 친 v칝re ganske greit
n친r man har tatt en liten titt p친 det.

:blog-post/body

Datalog er et logisk sp칮rrespr친k som ligger et sted mellom Prolog og SQL, noe
som unektelig h칮res ganske esoterisk ut, men som viser seg 친 v칝re ganske greit
n친r man har tatt en liten titt p친 det.

### Kode vs sp칮rring

I en [tidligere bloggpost](/alle-gatene-i-kommunen/) i denne serien s친 vi dette
kodeeksempelet:

```clj
(:poststed/navn
 (d/entity db [:poststed/postnummer "1630"]))

;; => "Gamle Fredrikstad"
```

Datomic st칮tter ogs친 친 finne data ved hjelp av Datalog, hvor sp칮rringen ville
sett slik ut:

```clj
[:find ?navn .
 :where
 [?poststed :poststed/postnummer "1630"]
 [?poststed :poststed/navn ?navn]]
```

Hva skjer her egentlig? Jo, vi ber om 친 finne 칠n verdi, som vi kaller `?navn`.

- Sp칮rsm친lstegnet i starten av symbolet indikerer til Datalog at dette er en
  variabel.
- Punktumet `.` indikerer at vi bare vil ha 칠n verdi, ikke flere.

S친 ramser vi opp noen klausuler som m친 oppfylles. Disse f칮lger m칮nsteret vi
kjenner fra [f칮rste bloggpost](/smak-av-datomic/) i serien:

```
[entitet, attributt, verdi]
```

Alts친,

- Finn en entitet `?poststed` med attributtet `:poststed/postnummer` med
  verdi `"1630"`.

- Finn denne samme entiteten `?poststed` sitt attributt `:poststed/navn`, og bind `?navn` til verdien av attributtet.

S친 kan vi be Datomic gjennomf칮re sp칮rringen, slik:

```clj
(d/q '[:find ?navn .
       :where
       [?poststed :poststed/postnummer "1630"]
       [?poststed :poststed/navn ?navn]]
     db)

;; => "Gamle Fredrikstad"
```

Her kaller vi funksjonen `datomic.api/q` med sp칮rringen v친r og databasen `db`,
og f친r svaret tilbake. Fortsatt Gamle Fredrikstad!

### En forvikling i datamodellen

I tiden siden den forrige bloggposten ble skrevet, s친 har datamodellen v친r
endret seg. Vi oppdaget at "poststed" var en fullverdig entitet som kunne ha
flere postnummere.

Alts친:

- Et poststed er identifisert av sitt navn.
- Hvert poststed kan ha mange postnummere.

Med denne oppdaterte modellen, s친 ser sp칮rringen v친r s친nn ut:

```clj
(d/q '[:find ?navn .
       :where
       [?postnummer :postnummer/nummer "1630"]
       [?postnummer :postnummer/poststed ?poststed]
       [?poststed :poststed/navn ?navn]]
     db)

;; => "Gamle Fredrikstad"
```

Her ser vi at det er tre klausuler.

- Det finnes en entitet `?postnummer` med `:postnummer/nummer` med verdi `"1630"`.
- Den samme entiteten `?postnummer` har en referanse til en annen entitet
  `?poststed` via `:postnummer/poststed`-attributtet. Det er en slags join.
- Denne andre entiteten `?poststed` har et `?navn`.

Merk at alle variablene er navn som vi har funnet p친. De kunne
like gjerne v칝rt `?a`, `?b` og `?c`:

```clj
(d/q '[:find ?a .
       :where
       [?b :postnummer/nummer "1630"]
       [?b :postnummer/poststed ?c]
       [?c :poststed/navn ?a]]
     db)

;; => "Gamle Fredrikstad"
```

Poenget her er at de brukes til 친 vise hvilke deler av `[e, a, v]`-triplene som
er de samme. De binder klausulene sammen.

## Parameteriserte sp칮rringer

Vi kan ogs친 lage oss en sp칮rring som tar imot postnummeret. Det kan jo tenkes at
vi lurer p친 noen andre steder ogs친. Da ser det slik ut:

```clj
(d/q '[:find ?navn .
       :in $ ?nummer
       :where
       [?postnummer :postnummer/nummer ?nummer]
       [?postnummer :postnummer/poststed ?poststed]
       [?poststed :poststed/navn ?navn]]
     db "1630")

;; => "Gamle Fredrikstad"
```

I tillegg til `:find` og `:where`, s친 har vi n친 `:in`. Denne seksjonen
beskriver inn-parameterene. Legg merke til at `d/q` tar to parametere i tillegg
til selve sp칮rringen: `db` og `"1630"`. Dette speiles av `$` og `?nummer`.
Dollartegnet er alts친 databasen. Mer om det i en senere bloggpost.

N친 som vi har parameterisert sp칮rringen, kan vi lage oss en funksjon:

```clj
(defn finn-stedsnavn-p친-postnummer [db nummer]
  (d/q '[:find ?navn .
         :in $ ?nummer
         :where
         [?postnummer :postnummer/nummer ?nummer]
         [?postnummer :postnummer/poststed ?poststed]
         [?poststed :poststed/navn ?navn]]
       db nummer))

(finn-stedsnavn-p친-postnummer db "1605") ;;=> "Fredrikstad"
(finn-stedsnavn-p친-postnummer db "1412") ;;=> "Sofiemyr"
```

Men la meg stille deg et sp칮rsm친l: Er det egentlig n칮dvendig 친 pakke denne
fine datastrukturen inn i en opak funksjon? Er ikke noe av poenget med et
sp칮rrespr친k at det er deklarativt og inspiserbart? At det er data?

La oss pr칮ve p친 det:

```clj
(def finn-stedsnavn-p친-postnummer
  '[:find ?navn .
    :in $ ?nummer
    :where
    [?postnummer :postnummer/nummer ?nummer]
    [?postnummer :postnummer/poststed ?poststed]
    [?poststed :poststed/navn ?navn]])

(d/q finn-stedsnavn-p친-postnummer db "1630") ;;=> "Fredrikstad"
(d/q finn-stedsnavn-p친-postnummer db "1412") ;;=> "Sofiemyr"
```

Rock 'n roll! 游뱆

## En sjokkerende tvist p친 slutten

Hva om vi har lyst til 친 finne alle postnummere p친 et poststed?

```clj
(def finn-postnummere-for-stedsnavn
  '[:find [?nummer ...]
    :in $ ?navn
    :where
    [?postnummer :postnummer/nummer ?nummer]
    [?postnummer :postnummer/poststed ?poststed]
    [?poststed :poststed/navn ?navn]])

(d/q finn-postnummere-for-stedsnavn db "Gamle Fredrikstad")

;;=> ["1636" "1634" "1633" "1632" "1630" "1639" "1637"]
```

Hvordan skiller denne sp칮rringen seg fra den forrige?

- Istedenfor 친 finne `?navn .` (ett navn), s친 finner vi n친 `[?nummer ...]` (flere nummere).

- Vi tar imot `?navn` som parameter istedefor `?nummer`.

Men hva med klausulene?

De er s칮renmeg kliss like!

Vi beskriver jo den samme sammenhengen, bare med en annen ukjent.

M. Night Shyamalan, ta deg en bolle!
