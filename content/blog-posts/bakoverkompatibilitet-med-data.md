:page/title Bakoverkompatibilitet med data
:blog-post/author {:person/id :person/teodor}
:blog-post/published #time/ldt "2025-08-05T11:07:30.305284"
:blog-post/tags [:bakoverkompatibilitet :data]
:blog-post/description

En historie om hvordan dataorientering muliggj칮r bakoverkompatibilitet, fortalt med SI-enheter.

:open-graph/description

En historie om hvordan dataorientering muliggj칮r bakoverkompatibilitet, fortalt med SI-enheter.

:blog-post/body

Er det ikke deilig n친r en bit av koden bare er *ferdig*?

Christian har tidligere skrevet om bakoverkompatibilitet i
[Slutt 친 칮delegge tingene mine](/bakoverkompatibilitet/)
og i [Hvordan jeg l칝rte 친 slutte 친 bekymre meg og elske API-et](/bakoverkompatibilitet-i-praksis/).
I dag skal jeg fortelle en liten historie om Munit, et lite system for 친 regne p친 tall med enhet.
Systemet startet *litt* dataorientert, som gjorde det enklere 친 endre, og endte *mer* dataorientert, som ga flere fine effekter.

Da jeg inns친 at jeg kunne gj칮re endringen uten 친 endre public API-et mitt fikk jeg en god f칮lelse av ro!
驕쥞잺游땗

## Dagens eksempel: ekvivalente personbiler for s칮ylelast

Hvor mye er egentlig 17 Mega-Newton (MN)?

I byggingen av [Clarion Hotel The Hub] i Oslo Sentrum i 2017, kom jeg over en stor s칮ylelast.

![Clarion Hotel The Hub](/images/the-hub.jpg)

Hotellet skulle f친 (har) en stor, fin konferansesal, cirka 24 meter i bredden.
Hvis jeg hadde fylt konferansesalen med en s칮ylepark, ville salen sett ut som en parkeringskjeller.
Den hadde blitt vanskelig 친 bruke.
Tenk, du danser vilt rundt og br칮ler til Avril Lavigne:

> 游꿪 游꿨<br>
> He was a skater boy.<br>
> She said, "See ya later, boy."<br>
> He wasn't good enough for her.<br>
> 游꿪 游꿨

... f칮r du tryner baklengs inn i en noks친 hard st친ls칮yle.

For 친 unng친 det, bar vi kreftene fra hotellrommene over ut til en usynlig s칮ylerad i veggene p친 siden av salen.
Noe s친nt:

![Skisse av s칮ylen med de 17 MN](/images/the-hub-skisse-soyle-17mn.png)

<style type=text/css>
/* Begrens bilder til tekstbredden */
img {
    max-width: 100%;
}
</style>

To s칮yler endte opp med 친 m친tte t친le 17 MN trykk hver.
Men hvor mye er det egentlig, i st칮rrelser vi har et forhold til?

La oss pr칮ve 친 regne om til personbiler: hvor h칮yt m친 vi stable personbiler for 친 f친 samme vekt?

[Clarion Hotel The Hub]: https://www.strawberry.no/hotell/norge/oslo/clarion-hotel-the-hub/

```clojure
(require '[munit.prefix :refer [k M]]
         '[munit.si :refer [kg m s]]
         ;; OBS: * og / kommer fra munit.units, ikke clojure.core.
         '[munit.units :refer [* /]])

;; ett tonn er 1000 kilo
(def t (* 1000 kg))
t
;; => [1000 kg]

;; en personbil veier cirka et tonn.
(def personbil-masse [1 t])

;; SI-enheten Newton er kilogram-meter per sekund i annen
(def N [kg m {s -2}])
;; les denne som "kilogram meter per sekund i annen". (-2 er eksponenten til sekund)
;; (m친let er at tall med enhet skal v칝re naturlige 친 skrive!)

;; 1 kilo-Newton er 1000 Newton, og 1 Mega-Newton er 1 000 000 Newton.
(def kN [k N])
(def MN [M N])

;; ... der k og M er vanlige Clojure-tall fra munit.prefix.
k
;; => 1000
M
;; => 1000000

(def g "omtrentlig tyngdeakselerasjon i Norge"
  ;; SI-enheten for aksellerasjon er (meter per sekund) per sekund
  ;; ... alts친 endring i fart per tid.
  [9.8 m {s -2}])

(def personbil-tyngekraft (* personbil-masse g))
(def stor-s칮ylelast [17 MN])
(def ekvivalente-personbiler (/ stor-s칮ylelast personbil-tyngekraft))

ekvivalente-personbiler
;; => 1734.6938775510205
```

S칮ylen m친tte alts친 t친le 친 b칝re 1700 personbiler stablet opp친 hverandre.
Det er en h칮y stabel!

## Dataorienterte API-er

Munit er et lite bibliotek i prototype-fase som lar deg jobbe med tall som har SI-enhet, eller en annen enhet.
Biblioteket er dataorientert, i den forstand at du sender inn vanlige Clojure-datastrukturer, og f친r ut Clojure-datastrukturer.
Det f칮lger ogs친 med noen vanlige konstanter.

```clojure
(require '[munit.si :refer [m]]
         '[munit.units :refer [* /]])
         
;; Clojure-tall tolkes som enhetsl칮se st칮rrelser
(def pi 3.1415)

;; Maps lar deg definere nye enheter av eksponenter
(def m3 {m 3})

;; Vektorer impliserer multiplikasjon
(def mm (/ m 1000))
(def r [30 mm])
(def h [3 m])
(def sylinder-volum (* pi r r h))
```

Det er alt!
N친 kan du biblioteket: Lag tall med SI-enheter av vanlige Clojure-tall, vektorer, maps eller base-enheter, bruk s친 operasjonene som f칮lger med.

## F칮r endringen: munit implementert med defrecord.

Etter to uker i "[hengek칮ya]", skurret f칮lgende for meg i implementasjonen:

[hengek칮ya]: https://www.youtube.com/watch?v=f84n5oFoZBc

```clojure
(ns munit.impl
  "Details for working with units. Do not use directly."
  (:require clojure.pprint
            munit.runtime))

;; [3]
(set! *warn-on-reflection* true)

;; [1]
(defrecord BaseUnit [system sym])
(defrecord Quantity [magnitude exponents])

;; 游녢 [2]                         游녢 [3]              游녢 [3]
(defmethod print-method BaseUnit [^BaseUnit base-unit ^java.io.Writer w]
  (.write w (pr-str (.sym base-unit))))

(defmethod clojure.pprint/simple-dispatch BaseUnit [^BaseUnit base-unit]
  (clojure.pprint/write-out (.sym base-unit)))

;; [4] Reload SI units after redefining records.
(when (and munit.runtime/dev?
           (contains? (loaded-libs) 'munit.si))
  (require 'munit.si :reload))
```

游땳

1. Trenger vi egentlig typer for BaseUnit og Quantity?
2. Hvorfor m친 vi dille med print-method for 친 forkle BaseUnit som symboler?
3. Typehint for 친 unng친 reflection er litt kjedelig.
4. ... og hvorfor m친 vi drive og passe p친 at vi laster SI-navnerommet med `def`-er av BaseUnit-er p친 nytt n친r vi redefinerer BaseUnit-typen?

Dette m친 da kunne l칮ses p친 en mindre vond m친te.

## Etter endringen: data hele veien ned.

Records viste seg 친 v칝re en d친rlig id칠!

Jeg innf칮rte typene da jeg ikke klarte 친 implementere +, -, * og / lett uten 친 ha en "kanonisk" representasjon for tall med enhet.
Overalt m친tte jeg finne st칮rrelsen ("magnitude") og enheten ("unit") til tall.

Det problemet kunne jeg i stedet l칮st ved 친 lage to funksjoner, `magnitude` og `unit`!

```clojure
;; F칮rst implementasjonsdetaljene.
;; Scroll forbi hvis du vil, denne kodesnutten er kun med i tilfelle folk lurer.

;; Hvis du ikke scroller forbi, anbefaler jeg 친 lese `magnitude`, `unit` og
;; `simplify`. De er viktigst!

(ns munit.impl
  "Unit arithmetic implementation details, do not use directly."
  (:refer-clojure :exclude [+ - * /]))

(defn magnitude [x]
  (cond (number? x)
        x

        (vector? x)
        (reduce clojure.core/* (map magnitude x))

        ;; Otherwise, it's a unit, magnitude is 1.
        :else
        1))

(defn remove-vals [m pred?]
  (reduce (fn [m' [k v]]
            (cond-> m'
              (pred? v)
              (dissoc k)))
          m
          m))

(defn mul-units [u1 u2]
  (merge-with clojure.core/+ u1 u2))

(defn unit [x]
  (-> (cond (number? x)
            {}

            (vector? x)
            (reduce mul-units (map unit x))

            (map? x)
            x

            ;; Otherwise, assume a base unit.
            :else
            {x 1})
      (remove-vals zero?)))

(defn simplify [x]
  (cond
    ;; Enhetsl칮se tall forenkler til kun tallet
    (every? zero? (vals (unit x)))
    (magnitude x)

    ;; Tall med st칮rrelse 1 forenkler til kun enheten
    (and (= 1 (magnitude x))
         (every? #(not= 1 %) (vals (unit x))))
    (unit x)

    ;; for 칮vrige tall gir vi en vektor av faktorer.
    :else
    (->> [[(magnitude x)]
          (->> (unit x)
               (filter (comp #{1} second))
               (map first))
          (some->> (unit x)
                   (remove (comp #{0 1} second))
                   (into {})
                   not-empty
                   vector)]
         (into [] cat))))

(defn map-vals [m f]
  (reduce (fn [m' [k v]]
            (assoc m' k (f v)))
          {}
          m))

(def negate-vals #(map-vals % clojure.core/-))

(defn invert [x]
  (simplify [(clojure.core// (magnitude x))
             (negate-vals (unit x))]))

(defn div [x y]
  (simplify [(clojure.core// (magnitude x)
                             (magnitude y))
             (merge-with clojure.core/+
                         (unit x)
                         (negate-vals (unit y)))]))

(defn same-unit? [x y]
  (= (unit x) (unit y)))

(defn add [x y]
  (when-not (same-unit? x y)
    (throw (ex-info "Cannot add quantities of different units"
                    {:x x :y y})))
  (simplify
   [(clojure.core/+ (magnitude x) (magnitude y))
    (unit x)]))

(defn negate [x]
  (simplify [(clojure.core/- (magnitude x))
             (unit x)]))

(defn sub [x y]
  (when-not (same-unit? x y)
    (throw (ex-info "Cannot subtract quantities of different units"
                    {:x x :y y})))
  (simplify
   [(clojure.core/- (magnitude x) (magnitude y))
    (unit x)]))
```

... og her er API-et for folk!

```clojure
(ns munit.units
  (:refer-clojure :exclude [* / + -])
  (:require [munit.impl :refer [simplify invert div add negate sub]]))

(defn *
  ([] 1)
  ([x] x)
  ([x y] (simplify [x y]))
  ([x y & args]
   (simplify [x y (vec args)])))

(defn /
  ([x] (invert x))
  ([x y] (div x y))
  ([x y & args]
   (reduce div (div x y) args)))

(defn +
  ([] 0)
  ([x] x)
  ([x y] (add x y))
  ([x y & args]
   (reduce add (add x y) args)))

(defn -
  ([x] (negate x))
  ([x y] (sub x y))
  ([x y & args]
   (reduce sub (sub x y) args)))

(defn measure-in [x target-unit]
  (let [converted (/ x target-unit)]
    (when-not (number? converted)
      (throw (ex-info "Cannot convert to target unit"
                      {:quantity x
                       :target-unit target-unit
                       :leftover converted})))
    converted))
```

## Bakoverkompatibilitet med data

Overgangen fra records til data har gitt meg flere forbedringer:

- Mindre kode
- `def`-er for st칮rrelser (Quantity) og base-enheter (BaseUnit) kan ikke lenger komme ut av synk med typedefinisjoner
- Du velger hvordan du vil ha base-enhet selv, for eksempel med n칮kkelord eller symboler:
    ```clojure
    ;; velg hvordan du vil skrive base-enheter selv!
    :si/m :m 'm 'si/m
    ;; ... s친 lenge de kan sammenliknes med =.
    ```
- Printing og serialisering av data er trivielt (vanlig Clojure-data kan printes) og umagisk (ingen records som later som de er symboler).

... og denne overgangen kunne jeg gj칮re uten 친 brekke public-API-et mitt!
Kodesnutten med de 17 MN var uendret mellom gammelt API og nytt API.

Dette er en ny m친te 친 tenke API-design for meg.
Det har v칝rt til stor hjelp 친 se Christian jobbe med Replicant og Nexus.
Stegene blir noe s친nt:

- Hvilke datastrukturer er hyggelige 친 skrive inn fra REPL?
- Finn operasjonene som folk skal bruke (`* / + -`).
- ... og finn s친 funksjonene som trengs for 친 lage API-et (`simplify`, `magnitude` og `unit`).

Sam Ritchie spurte en gang Gerald Sussman om hvordan han s친 p친 formlene sine, om han brukte TeX.
Sussman svarte at han bare s친 dataene.
(For Sussman var data alltid lister.
 Sussman har jobbet mest i Scheme, og (faktisk) sammen med Guy Steele skrevet Scheme, et spr친k som har hatt stor innflytelse p친 Clojure.)
For meg ligger det noe viktig akkurat her: med en konsis og god datastruktur, blir den datastukturen i seg selv notasjon man "ser".
I dag ser du kanskje `[:em heisann!]` like godt som `<em>heisann!</em>`.
I morgen ser du kanskje `[9.8 m {s -2}]` like godt som `9.8 m/s`?

## Interoperabilitet med data

N친 som munit ikke lenger krever noen bruk av spesifikke typer, passer munit mye bedre inn i andre systemer.
Det Hiccup har gjort for HTML og det Ring har gjort for HTTP-requests og HTTP-responses kan kanskje Munit gj칮re for tall med SI-enhet.
Med en datanotasjon (en datastruktur) p친 plass, kan forskjellige biblioteker jobbe p친 samme datastruktur.

## To funksjoner erstattet typer i "midjen" til biblioteket

Veldig mange gode biblioteker er orientert rundt en "smal midje".
I Clojure-칮kosystemet kjenner du kanskje allerede midjene [Hiccup] for HTML og [Ring] for HTTP.
[Pandoc] er en fin midje for dokumenter.

[Hiccup]: https://github.com/weavejester/hiccup
[Ring]: https://github.com/ring-clojure/ring
[Pandoc]: https://pandoc.org/

Vi trenger *midjer* for 친 unng친 dobbeltarbeid.
Forfatteren av Oil Shell sier det godt i [The Internet Was Designed With a Narrow Waist]:

> It avoids O(M 칑 N) code explosions, letting us write O(M + N) amounts of code instead.
> This is a big deal in practice! Most code is glue, but it doesn't have to be this way.

[The Internet Was Designed With a Narrow Waist]: https://www.oilshell.org/blog/2022/02/diagrams.html

I f칮rste versjon av munit, hadde jeg ingen typer.
Men f칮rste versjon ble aldri ferdig, fordi jeg fikk en MxN-eksplosjon i implementasjonen av *, /, + og -!
Alle m친tte plukke ut enhet og st칮rrelse uten hjelp.

Kvantitet-typen tok s친 over som midjen i munit-med-typer:

```clojure
(defrecord Quantity [magnitude exponents])
```

... f칮r jeg fant ut at jeg like gjerne kunne eksponere st칮rrelse (`magnitude`) og enhet (som f칮rst het `exponents`, f칮r den ble omd칮pt til `unit`) som vanlige funksjoner.

*Takk til Mathias, Sigmund og Lars Kristian, som leste tidlige versjoner av denne teksten.
Teksten er langt bedre n친, mye takket v칝re innspillene deres!
Takk til Colin Smith og Sam Ritchie for arbeidet p친 [Emmy].
Takk til Gerald Sussman og Chris Hanson for [Software Design for Flexibility], som skisserer ut arkitekturen som Munit f칮lger.
Takk til Anteo AS og Tormod Mathiesen for [Broch], et annet bibliotek for tall med enhet.*

[Software Design for Flexibility]: https://mitpress.mit.edu/9780262045490/software-design-for-flexibility/
[Emmy]: https://github.com/mentat-collective/emmy
[Broch]: https://github.com/anteoas/broch
