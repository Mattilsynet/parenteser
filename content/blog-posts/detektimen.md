:page/title Kode som skriver kode
:blog-post/author {:person/id :person/christian}
:blog-post/published #time/ldt "2024-08-15T09:00:00"
:blog-post/tags [:clojure :observability]
:blog-post/description

I verktÃ¸ykassa til Clojure finner vi blant annet makroer -- kode som skriver
kode. Ikke det fÃ¸rste man bÃ¸r ty til, men de kan by pÃ¥ elegante lÃ¸sninger. Som
de gjorde for oss denne uka.

:blog-post/body

Vi bygger for tiden [et nytt system](/utviklingsstrategi/). Rett fÃ¸r sommeren la
vi grunnlaget for [innsikt i produksjonsmiljÃ¸et
vÃ¥rt](https://www.honeycomb.io/blog/what-is-observability-key-components-best-practices).
Planen er Ã¥ bruke [OpenTelemetry
tracing](https://opentelemetry.io/docs/concepts/signals/traces/) heller enn
tradisjonell logging, av flere grunner:

- De bestÃ¥r av strukturerte data
- Gir mer kontekst enn frittstÃ¥ende logglinjer
- Vi fÃ¥r varighet pÃ¥ alle operasjoner
- Tracene blir nÃ¸sta, slik at vi kan se call stacken

I kode ser det sÃ¥nn ut:

```clj
(defn handle-import-message [ctx message]
  (tracing/with-span ["Handle import message" message]
    (doseq [decree (create-import-decrees message)]
      (commandant/issue ctx decree))))
```

Her behandler vi en [import-melding fra en kÃ¸](/nats-import-eksport/).
`tracing/with-span` nÃ¸ster rundt koden. Videre bruk av `tracing/with-span`
dypere inn i koden vil da legge seg innunder den gjeldende span-en (`"Handle
import message"`) og gi oss en fossefallsoversikt over kallene, slik [Magnar
beskrev tidligere i vÃ¥r](/85-sekunder/).

Enn sÃ¥ lenge har vi bare plassert traces strategisk rundt inngangspunktene i
kodebasen: nÃ¥r vi tar imot en web request eller plukker en melding fra en kÃ¸ sÃ¥
starter vi med Ã¥ bygge et nytt trace. Dette gir oss traces for absolutt alt
systemet vÃ¥rt gjÃ¸r. Sammen med noen velplasserte trace-kall ellers i kodebasen
har vi allerede ganske god oversikt over hvor tiden gÃ¥r, om enn noe grovkornet.

## Repetetivt arbeid

Tidligere i uka lurte vi pÃ¥ hvorfor enkelte importer tok sÃ¥ lang tid. Mer
finkornede traces ville vÃ¦rt til hjelp, sÃ¥ vi bestemte oss for Ã¥ instrumentere
en haug med funksjoner. Det ble fort tydelig at dette var en noksÃ¥ repetetiv
oppgave:

```clj
(defn vask-navn [navn & [senter]]
  (tracing/with-span ["vask-navn" {:senter senter}]
    ,,,))

(defn finn-senter [state {:keys [NAVN adresse]}]
  (tracing/with-span ["finn-senter" {:state state
                                     :NAVN NAVN
                                     :adresse adresse}]
    ,,,))

(defn hent-senter-adresse-ref [senter]
  (tracing/with-span ["hent-senter-adresse-ref" {:senter senter}]
    ,,,))
```

Dette er sÃ¥ repetetivt arbeid at det er lite egnet for menneskehender. Og det er
akkurat for tilfeller som dette -- kode som er formmessig lik -- at Clojure sine
makroer kan vÃ¦re til hjelp.

## Hva er en makro?

En makro er en kodesnutt som skriver kode til compileren. Makroen kjÃ¸rer idet
koden blir kompilert. Det er det litt vanskelig Ã¥ vri hodet sitt rundt, sÃ¥ la
oss starte med et eksempel.

Clojure har `when`, en slags `if` uten `else`, men ingen `unless`. SÃ¥ la oss
lage den:

```clj
(defmacro unless [test expr]
  `(when (not ~test)
     ~expr))
```

Vi kan bruke den som dette:

```clj
(unless (.exists (io/file "deps.edn"))
  (spit "deps.edn" "{}"))
```

Hvis `unless` var en helt vanlig funksjon, sÃ¥ ville `(spit "deps.edn" "{}")`
blitt evaluert pÃ¥ vei inn i funksjonskallet. Da ville vi med andre ord
overskrevet fila, enten den finnes eller ikke. Ikke helt ideelt.

Med makroen vÃ¥r fÃ¥r vi heller _koden_ som argument, og kan returnere koden vi
Ã¸nsker at compileren skal jobbe videre med.

Eksempelet vÃ¥rt blir dermed skrevet om til:

```clj
(when (not (.exists (io/file "deps.edn")))
  (spit "deps.edn" "{}"))
```

## Og nÃ¥: Detektimen

I en Lisp er (nesten) alt enten en funksjon eller en makro. Eksempelvis er
`defn` en makro som kombinerer `def` og `(fn [] ,,,)`. IdÃ©en vÃ¥r var Ã¥ lage en
`defn` som automatisk tok seg av tracing. Etter litt diskusjon landet vi pÃ¥
dette billedlige API-et:

```clj
(defnğŸ•µï¸â€â™‚ï¸ vask-navn [navn & [senter]]
  ,,,)

(defnğŸ•µï¸â€â™‚ï¸ finn-senter [state {:keys [NAVN adresse]}]
  ,,,)

(defnğŸ•µï¸â€â™‚ï¸ hent-senter-adresse-ref [senter]
  ,,,)
```

AltsÃ¥: hvis du Ã¸nsker Ã¥ trace en funksjon er det bare Ã¥ "slenge pÃ¥ detektiven".
Vi syns det var ganske kostelig. Men hvordan ser implementasjonen ut? Hold deg
fast:

```clj
(defmacro defnğŸ•µï¸â€â™‚ï¸ [fn-name & forms]
  (let [[docstring arg-list & body] (if (string? (first forms))
                                      forms
                                      (cons "" forms))]
    `(defn ~fn-name
       ~docstring
       ~arg-list
       (with-span [~(str *ns* "/" fn-name)]
         ~@body))))
```

Som du kanskje kan fÃ¥ Ã¸ye pÃ¥ innimellom [all
syntaksen](https://clojure-doc.org/articles/language/macros/), lager `defnğŸ•µï¸â€â™‚ï¸` en
helt vanlig `defn`, men med et kall til `with-span` rundt koden i funksjonen.
SÃ¦rs observante lesere vil legge merke til at denne varianten ikke tracer
argumentene, slik vi gjorde manuelt lenger opp. Vel...

## Les videre pÃ¥ egen risiko

Vi skrev opprinnelig en variant som ogsÃ¥ tracet argumentene. Den forstod
destrukturering, og alt mulig. Vi endte opp med Ã¥ droppe det, ettersom all data
allerede er tilgjengelig pÃ¥ den ytterste span-en i tracet. La oss ta en kikk pÃ¥
hvordan det kan lÃ¸ses.

FÃ¸rst har vi denne hjelpefunksjonen:

```clj
(defn get-syms [xs]
  (->> xs
       (tree-seq coll? (fn [x]
                         (cond-> x
                           (and (map? x) (:as x))
                           (select-keys [:as]))))
       (filter symbol?)
       (remove #{'&})))
```

Den finner alle symbolene i en vilkÃ¥rlig datastruktur, som feks en
argumentliste:

```clj
(get-syms '[senter]) ;;=> (senter)
```

Den ignorerer `&`, som brukes til Ã¥ samle rest-argumenter, og finner nÃ¸sta
symboler:

```clj
(get-syms '[navn & [senter]]) ;;=> (navn senter)
```

Den "forstÃ¥r" destrukturering:

```clj
(get-syms '[state {:keys [NAVN adresse]}]) ;;=> (state NAVN adresse)
```

Dersom noe er strukturert, men ogsÃ¥ bundet i sin helhet med `:as` trenger vi
bare helheten:

```clj
(get-syms '[state {:keys [NAVN adresse] :as payload}]) ;;=> (state payload)
```

Ved Ã¥ kalle den fra makroen vÃ¥r med argumentlista til funksjonen vi definerer,
kan vi gjenskape det manuelle eksempelet til punkt og prikke:

```clj
(defn get-syms [xs]
  (->> xs
       (tree-seq coll? (fn [x]
                         (cond-> x
                           (and (map? x) (:as x))
                           (select-keys [:as]))))
       (filter symbol?)
       (remove #{'&})))

(defmacro defnğŸ•µï¸â€â™‚ï¸
  {:clj-kondo/lint-as 'clojure.core/defn}
  [fn-name & forms]
  (let [[docstring arg-list & body] (if (string? (first forms))
                                      forms
                                      (cons "" forms))]
    `(defn ~fn-name
       ~docstring
       ~arg-list
       (with-span [~(str *ns* "/" fn-name)
                   ~(into {} (for [sym (get-syms arg-list)]
                               [(keyword sym) sym]))]
         ~@body))))
```

Som en liten bonus har jeg ogsÃ¥ lagt pÃ¥ et hint til
[clj-kondo](https://github.com/clj-kondo/clj-kondo) om at bruk av `defnğŸ•µï¸â€â™‚ï¸` skal
forstÃ¥s som bruk av helt vanlig `defn`.

Hvis du hang med helt hit, sÃ¥ kan vi bare gratulere. Heldigvis er dette lite
representativt for Clojure-koden vi skriver, men det er utrolig kult Ã¥ kunne
gjÃ¸re ved behov.
