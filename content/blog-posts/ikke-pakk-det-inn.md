:page/title Ikke pakk inn godsakene
:blog-post/author {:person/id :person/magnar}
:blog-post/published #time/ldt "2024-12-10T09:00:00"
:blog-post/tags [:clojure :design]
:blog-post/description

Jula n칝rmer seg, og det er klart: Gaver m친 pakkes. Men det er ingen
presang til de andre p친 teamet om du pakker inn funksjonaliteten du skriver
i for mye glorete papir.

:blog-post/body

Jula n칝rmer seg, og det er klart: Gaver m친 pakkes. Men det er ingen
presang til de andre p친 teamet om du pakker inn funksjonaliteten du skriver
i for mye glorete papir.

Her er et eksempel. Det er en liten munnfull, s친 ikke heng deg opp i detaljene.
Kort fortalt s친 oppdaterer funksjonen poststedet p친 en adresse, slik at b친de
`"mo말rana"` og `"MOIRANA"` blir til det normaliserte `"Mo말Rana"`:

```clj
(defn normaliser-poststed [adresse]
  (assoc adresse :poststed
         (->> (for [ord (map str/lower-case (str/split (:poststed adresse) #" "))]
               (if (#{"og" "i" "p친"} ord)
                 ord
                 (->> (str/split ord #"-")
                      (map str/capitalize)
                      (str/join "-"))))
             (str/join " "))))
```

Kan du se hva som er feil? Her er det noen godsaker, men de er pakket inn.
Funksjonen gj칮r en antagelse. Den antar at den alltid skal jobbe p친 en adresse -- alltid et poststed.

Hva om jeg har lyst til 친 normalisere et stedsnavn som ikke er poststedet til en
adresse? La oss rive av julepapiret:

```clj
(defn normaliser-stedsnavn [s]
  (->> (for [ord (map str/lower-case (str/split s #" "))]
         (if (#{"og" "i" "p친"} ord)
           ord
           (->> (str/split ord #"-")
                (map str/capitalize)
                (str/join "-"))))
       (str/join " ")))

(defn normaliser-poststed [adresse]
  (update adresse :poststed normaliser-stedsnavn))
```

Se s친, mye bedre! N친 er godsakene dratt ut til en gjenbrukbar funksjon, og
`normaliser-poststed` bruker den til 친 oppdatere poststedet p친 en adresse.

### Julepapirets skjebne

Ja, for hva gj칮r man egentlig med julepapiret? P친 fredag hadde jeg bes칮k av
renovasjonsavdelingen i Fredrikstad Kommune, som bandt en stor rosa pose p친
s칮ppeldunken min. "Julepapir" stod det p친 den.

For trenger vi egentlig `normaliser-poststed` n친?

Jeg kan like gjerne skrive dette selv:

```clj
(update adresse :poststed normaliser-stedsnavn)
```

Jeg trenger ikke at den er pakket inn for meg.

## Et par andre gjengangere

Hvis du tar deg selv i 친 v칝re behjelpelig og pakke inn godsakene med en standard
Clojure funksjon, s친 er det antagelig mer hjelpsomt 친 la v칝re.

Her er noen flere eksempler:

```clj
(wrap-gifts gifts)

;; =>

(map wrap-gift gifts)
```

Jeg trenger ikke hjelp til 친 kalle samme funksjon p친 mange elementer i en liste.
Lag funksjonen for 친 pakke inn 칠n pakke, s친 bruker jeg `map` n친r jeg har mange 친
pakke inn. 游꾸 游땏

```clj
(remove-naughty-kids kids)

;; =>

(remove naughty? kids)
```

Se, det holder med en `naughty?`-funksjon! Ikke bare leser den andre varianten
bedre, men n친 kan jeg bruke den med b친de `filter` og `remove` ved behov.

## Til slutt

Det er masse fine byggeklosser i [clojure.core](/clojure-core/). Lag nyttige sm친
funksjoner, uten 친 pakke dem inn, s친 kommer de til sin rett. Blir s친 bra, atte.

Og god jul da, du!
